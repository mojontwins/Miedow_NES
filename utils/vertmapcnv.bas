' vertmapcnv.bas
' Converts 16x256 map to 8x256 bytes packed.

Dim as uByte d1, d2, d 
Dim as Integer f1, f2
Dim as String cln
Dim as Integer ct
Dim as Integer adj

If Len (Command (3)) = 0 Then 
	Print "vertmapcnv in.map out.s label [-1]"
	System
End If

f1 = FreeFile
Open Command (1) For Binary as #f1
f2 = FreeFile
Open Command (2) For Output as #f2

If Command (4) = "-1" then adj = -1 else adj = 0

Print #f2, "; " & Command (2)
Print #f2, "; Generated by vertmapcnv"
Print #f2, "; Copyleft 2015 the Mojon Twins"
Print #f2, " "
print #f2, ".export _" & Command (3)
print #f2, " "
print #f2, "_" & Command (3) & ":"
ct = 0: cln = "    .byte "

While Not Eof (f1)
	Get #f1, , d1: d1 = d1 + adj
	Get #f1, , d2: d2 = d2 + adj
	d = (d1 And 15) * 16 + (d2 And 15) ' Safe
	cln = cln & "$" & Hex (d, 2)
	ct = ct + 1
	If ct = 8 Then
		Print #f2, cln
		ct = 0: cln = "    .byte "
	Else
		cln = cln & ", "
	End If
Wend

If ct <> 0 Then Print #f2, cln

Close
Print "Done."
