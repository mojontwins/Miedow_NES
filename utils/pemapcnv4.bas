' pemapcnv4.bas
' RLE maps for Pantanow Engine rev4.
' Copyleft 2017 by The Mojon Twins

#include "file.bi"
#include "fbpng.bi"
#include "fbgfx.bi"
#include once "crt.bi"

#include "cmdlineparser.bi"
#include "mtparser.bi"

Sub usage
	Print "$ pemapcnv4.exe in=mapa.map out=mapa.h [prefix=prefix] [height=height]"
	Print "                [fixmappy]"
	Print
	Print "Will convert mapa.map into mapa.h as a stream of rle'd bytes"
	Print "max metatile value should be 31 (32 different tiles)"
	Print
	Print "- If prefix is specified, map will be exported as map_prefix"
	Print "- If height is not specified, map will be 256 tiles high."
	Print "- fixmappy means substract 1 from each tile byte"
End Sub

Dim As Integer fIn, fOut
Dim As Integer ctr, value, lastValue
Dim As Integer i, j
Dim As uByte d
Dim As Integer buffer (255, 15)
Dim As uByte rleBuffer (4095)
Dim As Integer rleBufferIdx
Dim As Integer outputCt
Dim As Integer height
Dim As Integer fixmappy

Dim As String mandatory (1) = { "in", "out" }

sclpParseAttrs

If Not sclpCheck (mandatory ()) then
	usage
	End
End If

height = Val (sclpGetValue ("height"))
If height = 0 Or height > 256 Then height = 256

fIn = FreeFile
Open sclpGetValue ("in") For Binary As #fIn
fOut = FreeFile
Open sclpGetValue ("out") For Output As #fOut

fixmappy = (sclpGetValue ("fixmappy") <> "")

If lof (fIn) <> 16*height Then Print "Map should be 16x" & height & " metatiles, 1 byte per metatile!": Close: System

' Easier to work with a memory buffer
For i = 0 To (height-1)
	For j = 0 To 15
		Get #fIn, , d
		If fixmappy Then d = d - 1
		buffer (i, j) = (d And 31)
	Next j
Next i

' Compress & write
lastValue = -1 	' which is an illegal value
ctr = 0
rleBufferIdx = 0

For i = (height-1) To 0 Step -1
	For j = 0 To 15
		If lastValue = buffer (i, j) And ctr < 8 Then
			ctr = ctr + 1
		Else
			If lastValue <> -1 Then
				rleBuffer (rleBufferIdx) = ((ctr - 1) Shl 5) Or lastValue
				rleBufferIdx = rleBufferIdx + 1
			End If
			lastValue = buffer (i, j)
			ctr = 1
		End If
	Next j
Next i
rleBuffer (rleBufferIdx) = ((ctr - 1) Shl 5) Or lastValue
rleBufferIdx = rleBufferIdx + 1

Print #fOut, "// Pantanow Engine rev4 201706"
Print #fOut, "// Copyleft 2017 by The Mojon Twins"
Print #fOut, ""
Print #fOut, "// " & sclpGetValue ("out") & ", generated by pemapcnv3.exe"
Print #fOut, ""

If sclpGetValue ("prefix") <> "" then 
	Print #fOut, "const unsigned char map_" & sclpGetValue ("prefix") & " [] = {"
Else
	Print #fOut, "const unsigned char map [] = {"
End If

outputCt = 0
For i = 0 To rleBufferIdx - 1
	If outputCt = 0 Then Print #fOut, "	";
	Print #fOut, "0x" & Lcase (Hex (rleBuffer (i), 2));
	If i < rleBufferIdx - 1 Then Print #fOut, ", ";
	outputCt = outputCt + 1: If outputCt = 8 Then outputCt = 0: Print #fOut, ""
Next i

If outputCt Then Print #fOut, ""
Print #fOut, "};"

Close

Print "pemapcnv3 - " & rleBufferIdx & " bytes writen (vs " & (16*height) & ")."
