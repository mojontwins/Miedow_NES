' pemapcnv3.bas
' RLE maps for Pantanow Engine rev3.
' Copyleft 2017 by The Mojon Twins

Sub usage
	Print "$ pemapcnv3.exe mapa.map mapa.h [prefix]"
	Print
	Print "Will convert mapa.map into mapa.h as a stream of rle'd bytes"
	Print "max metatile value should be 31 (32 different tiles)"
	Print
	Print "Map should be 16x256 metatiles!"
End Sub

Dim As Integer fIn, fOut
Dim As Integer ctr, value, lastValue
Dim As Integer i, j
Dim As uByte d
Dim As Integer buffer (255, 15)
Dim As uByte rleBuffer (4095)
Dim As Integer rleBufferIdx
Dim As Integer outputCt

If Command (2) = "" Then usage: System

fIn = FreeFile
Open Command (1) For Binary As #fIn
fOut = FreeFile
Open Command (2) For Output As #fOut

If lof (fIn) <> 4096 Then Print "Map should be 16x256 metatiles, 1 byte per metatile!": Close: System

' Easier to work with a memory buffer
For i = 0 To 255
	For j = 0 To 15
		Get #fIn, , d
		buffer (i, j) = (d And 31)
	Next j
Next i

' Compress & write
lastValue = -1 	' which is an illegal value
ctr = 0
rleBufferIdx = 0

For i = 255 To 0 Step -1
	For j = 0 To 15
		If lastValue = buffer (i, j) And ctr < 8 Then
			ctr = ctr + 1
		Else
			If lastValue <> -1 Then
				rleBuffer (rleBufferIdx) = ((ctr - 1) Shl 5) Or lastValue
				rleBufferIdx = rleBufferIdx + 1
			End If
			lastValue = buffer (i, j)
			ctr = 1
		End If
	Next j
Next i
rleBuffer (rleBufferIdx) = ((ctr - 1) Shl 5) Or lastValue
rleBufferIdx = rleBufferIdx + 1

Print #fOut, "// Pantanow Engine rev3 201703"
Print #fOut, "// Copyleft 2017 by The Mojon Twins"
Print #fOut, ""
Print #fOut, "// " & Command (2) & ", generated by pemapcnv3.exe"
Print #fOut, ""

If Command (3) <> "" then 
	Print #fOut, "const unsigned char map_" & Command (3) & " [] = {"
Else
	Print #fOut, "const unsigned char map [] = {"
End If

outputCt = 0
For i = 0 To rleBufferIdx - 1
	If outputCt = 0 Then Print #fOut, "	";
	Print #fOut, "0x" & Lcase (Hex (rleBuffer (i), 2));
	If i < rleBufferIdx - 1 Then Print #fOut, ", ";
	outputCt = outputCt + 1: If outputCt = 8 Then outputCt = 0: Print #fOut, ""
Next i

If outputCt Then Print #fOut, ""
Print #fOut, "};"

Close

Print "pemapcnv3 - " & rleBufferIdx & " bytes writen (vs 4096)."
